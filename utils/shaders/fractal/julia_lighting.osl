struct Complex {
   float real;
   float imag;
};

Complex sum(Complex a, Complex b) {
    return Complex(a.real + b.real, a.imag + b.imag);
}

Complex mul(Complex a, Complex b) {
    return Complex(a.real*b.real - a.imag*b.imag, a.real*b.imag + a.imag*b.real);
}

float abs(Complex a) {
    return a.real*a.real + a.imag*a.imag;
}

Complex inv(Complex a) {
    return Complex(a.real/abs(a), -a.imag/abs(a));
}

Complex div(Complex a, Complex b) {
    return mul(a,inv(b));
}

shader JuliaSmooth(
    vector Position = vector(0, 0, 0),
    float seed_x = 0,
    float seed_y = 0,
    float bailout = 4,
    int max_iter = 64,
    output float inside = 1,
    output float prod = 1)
{

    float x = Position[0];
    float y = Position[1];
    Complex z = Complex(x,y);
    Complex c = Complex(seed_x, seed_y);
    Complex dz = Complex(1,0);
    Complex two = Complex(2,0);
    Complex temp = z;

    inside = 1;
    float iter = 0;

    while (inside && iter < max_iter) {
        if ( abs(z) > bailout ) { inside = 0; }
        else {
            temp = sum(mul(z,z),c);
            dz = mul(two,mul(z,dz));
            z = temp;
            iter ++;
        }
    }

    Complex w = div(z,dz);
    w = div(w,Complex(sqrt(abs(w)),0));

    prod = (w.real + w.imag) / sqrt(2) / 3.5 + 0.5;
}